config {
  type: "operations",
  hasOutput: true,
  schema: "holidays_in_japan__asia_northeast1"
}
CREATE OR REPLACE FUNCTION ${self()}(d DATE)
AS (
  "1973-04-12" <= d AND NOT ${ref("is_new_years_day")}(d) AND NOT ${ref("is_coming_of_age_day")}(d) AND
  NOT ${ref("is_national_foundation_day")}(d) AND NOT ${ref("is_emperors_birthday")}(d) AND
  NOT ${ref("is_vernal_equinox_day")}(d) AND NOT ${ref("is_showa_day")}(d) AND NOT ${ref("is_constitution_memorial_day")}(d) AND
  NOT ${ref("is_greenery_day")}(d) AND NOT ${ref("is_childrens_day")}(d) AND NOT ${ref("is_marine_day")}(d) AND
  NOT ${ref("is_mountain_day")}(d) AND NOT ${ref("is_respect_for_the_aged_day")}(d) AND
  NOT ${ref("is_autumnal_equinox_day")}(d) AND NOT ${ref("is_health_sports_day")}(d) AND
  NOT ${ref("is_sports_day")}(d) AND NOT ${ref("is_culture_day")}(d) AND NOT ${ref("is_labour_thanksgiving_day")}(d) AND
  ${ref("extra_holiday_name_if_holiday")}(d) IS NULL AND (
    SELECT
      IFNULL(LOGICAL_AND (${ref("is_new_years_day")}(day) OR ${ref("is_coming_of_age_day")}(day) OR
          ${ref("is_national_foundation_day")}(day) OR ${ref("is_emperors_birthday")}(day) OR ${ref("is_vernal_equinox_day")}(day) OR
          ${ref("is_showa_day")}(day) OR ${ref("is_constitution_memorial_day")}(day) OR ${ref("is_greenery_day")}(day) OR
          ${ref("is_childrens_day")}(day) OR ${ref("is_marine_day")}(day) OR ${ref("is_mountain_day")}(day) OR
          ${ref("is_respect_for_the_aged_day")}(day) OR ${ref("is_autumnal_equinox_day")}(day) OR
          ${ref("is_health_sports_day")}(day) OR ${ref("is_sports_day")}(day) OR ${ref("is_culture_day")}(day) OR
          ${ref("is_labour_thanksgiving_day")}(day) OR ${ref("extra_holiday_name_if_holiday")}(day) IS NOT NULL),
        FALSE)
    FROM
      UNNEST(GENERATE_DATE_ARRAY(d + 1 - EXTRACT(DAYOFWEEK FROM d), d - 1)) AS day
  )
);
